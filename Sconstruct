import sys

def RawString(name):
    def Impl(target, source, env):
        content = source[0].get_text_contents()
        with open(target[0].get_path(), 'w') as target_file:
            target_file.write("std::string {} = R\"~~~~({})~~~~\";".format(
                name,
                content
            ))
        return 0
    return Action(Impl, "Creating C++ raw string $TARGET from $SOURCE")

env = Environment()

skyShader = env.Command(
    'src/generated/sky.hh',
    'res/sky.frag',
    RawString('sky')
)
vertexShader = env.Command(
    'src/generated/vertex.hh',
    'res/vertex.vert',
    RawString('vertex')
)

libs = [
    'sfml-graphics',
    'sfml-system',
    'sfml-audio',
    'sfml-window'
]
cflags = ['-std=c++17']

if sys.platform != 'darwin':
    libs.append('GL')
else:
    cflags.append('-mmacosx-version-min=10.15')
    cflags.append('-stdlib=libc++')

main = env.Program(
    'main',
    Glob('src/*.cc'),
    LIBS=libs,
    FRAMEWORKS=['OpenGl'],
    CXXFLAGS=cflags
)

env.Requires(main, [skyShader, vertexShader])

Default(main)
